/**
 * Copyright (C) 2018 Dr. David H. Akehurst (http://dr.david.h.akehurst.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()       // for com.android.tools.build
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$version_abt"   // for com.android.application
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

subprojects {

    group 'net.akehurst.kaf.sample.helloworld'
    version '1.0.0-SNAPSHOT'

    apply plugin: "org.jetbrains.kotlin.multiplatform"

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }


    kotlin {
        targets {
            fromPreset(presets.jvm, 'jvm8') {
                compilations.all {
                    tasks[compileKotlinTaskName].kotlinOptions {
                        jvmTarget = '1.8'
                    }
                }
            }

            fromPreset(presets.js, 'js') {
                compilations.all {
                    tasks[compileKotlinTaskName].kotlinOptions {
                        moduleKind = 'plain'
                    }
                }
            }
            fromPreset(presets.macosX64, 'macos') {
                /* You can specify additional settings for the 'linux' target here */
            }

            // This preset is for iPhone emulator
            // Switch here to presets.iosArm64 (or iosArm32) to build library for iPhone device
            fromPreset(presets.iosX64, 'ios') {
                compilations.main.outputKinds('FRAMEWORK')
            }
        }
        sourceSets {
            commonMain {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:${version_kotlin}"
                }
            }
            commonTest {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-test-common:${version_kotlin}"
                    implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:${version_kotlin}"
                }
            }
            jvm8Main {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${version_kotlin}"
                }
            }
            jvm8Test {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-test:${version_kotlin}"
                    implementation "org.jetbrains.kotlin:kotlin-test-junit:${version_kotlin}"
                }
            }
            jsMain {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:${version_kotlin}"
                }
            }
            jsTest {
                dependencies {
                    implementation "org.jetbrains.kotlin:kotlin-test-js:${version_kotlin}"
                }
            }
            nativeCommonMain {
                dependencies {

                }
            }
            macosMain {
                dependencies {
                    dependsOn nativeCommonMain
                }
            }
            macosTest {
                dependencies {

                }
            }
        }
    }
}